pluginManagement {
    def flutterSdkPath = {
        def properties = new Properties()
        file("local.properties").withInputStream { properties.load(it) }
        def flutterSdkPath = properties.getProperty("flutter.sdk")
        assert flutterSdkPath != null, "flutter.sdk not set in local.properties"
        return flutterSdkPath
    }()

    includeBuild("$flutterSdkPath/packages/flutter_tools/gradle")

    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
    }
}

plugins {
    id "dev.flutter.flutter-plugin-loader" version "1.0.0"
    id "com.android.application" version "8.1.0" apply false
    id "org.jetbrains.kotlin.android" version "1.9.22" apply false
}

include ":app"

gradle.beforeProject { project ->
    project.afterEvaluate {
        if (project.hasProperty('android')) {
            // Fix namespace for AGP 8.x
            if (project.android.namespace == null) {
                def grp = project.group?.toString()
                if (grp != null && !grp.isEmpty()) {
                    project.android.namespace = grp
                }
            }
            // Force compileSdk 35 for lStar attr support
            def currentSdk = project.android.compileSdkVersion?.toString()
            if (currentSdk == null || !currentSdk.contains("35")) {
                project.android.compileSdk = 35
            }
        }
    }
}